plugins {
    id 'java-library'
    id("io.qameta.allure") version "2.11.2"
}

def allureVersion = "2.21.0",
    selenideVersion = "6.14.0"

allure {
    report {
        version.set(allureVersion)
    }
    adapter { // отвечает за появление папочки build/allure-results
        aspectjWeaver.set(true) //обработка аннотации @Step
        frameworks {
            junit5 { //название фреймворка
                adapterVersion.set(allureVersion) //версия интеграции фреймворка и Allure
            }
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation(
            'org.aspectj:aspectjrt:1.9.22',
            'org.aspectj:aspectjweaver:1.9.22',
            "com.codeborne:selenide:$selenideVersion",
            "org.junit.jupiter:junit-jupiter:5.10.0",
            "org.slf4j:slf4j-simple:2.0.7",
            "com.github.javafaker:javafaker:1.0.2",
            "io.qameta.allure:allure-selenide:2.29.1"
    )
}

tasks.withType(Test) {
    useJUnitPlatform()

    testLogging {
        lifecycle {
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}

task form_test(type: Test) {
    useJUnitPlatform {
        includeTags("form")
    }
}

task all_tests(type: Test) {
    useJUnitPlatform {
        includeTags("all_tests")
    }
}
